#include <Arduino.h>
#include <lvgl.h>
#include <esp32_smartdisplay.h>
#include "ui.h" // Include the header generated by SquareLine Studio

#include <WiFi.h>
#include <HTTPClient.h>

#define SDA_PIN 21
#define SCL_PIN 22
#define RST_PIN 23
#define IRQ_PIN 19

const char* ssid = "huawei";
const char* password = "s12345678";
const char* esp8266IP = "http://192.168.8.198/win"; // ESP8266 IP address

auto lv_last_tick = millis();

// Function to get the switch state
bool getSwitchState() {
    return lv_obj_has_state(ui_Switch1, LV_STATE_CHECKED);
}

// Function to send HTTP request
void toggleLED() {
    HTTPClient http;

    // Get switch state
    bool switchState = getSwitchState();
    
    // Create the full URL (T=1 for ON, T=0 for OFF)
    String url = String(esp8266IP) + "&T=" + (switchState ? "1" : "0");
    
    http.begin(url);
    int httpCode = http.GET();

    if (httpCode > 0) {
        Serial.println("LED toggled");
    } else {
        Serial.println("Error toggling LED");
    }

    http.end();
}

// Event callback when the switch state changes
void switch_event_cb(lv_event_t * e) {
    Serial.println("Switch state changed!");
    toggleLED();
}

void setup() {
    Serial.begin(115200);
    smartdisplay_init();

    // Connect to WiFi
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(1000);
        Serial.println("Connecting to WiFi...");
    }
    Serial.println("Connected to WiFi!");

    ui_init();

    // Attach event callback to switch
    lv_obj_add_event_cb(ui_Switch1, switch_event_cb, LV_EVENT_VALUE_CHANGED, NULL);
}

void loop() {
    auto const now = millis();
    
    // Update LVGL
    lv_tick_inc(now - lv_last_tick);
    lv_last_tick = now;
    lv_timer_handler();
}
